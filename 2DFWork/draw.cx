package draw

import "gl"

import "app"
import "entities"
import "geom"



var Black Color3
var White Color3
var Colors []Color3 // rainbow colors slice 


type Color3 struct {
	R f32
	G f32
	B f32
}



func Init () {
	str.print("2DFWork/draw.Init ()")
	
	Black = Color3{ R: 0.0, G: 0.0, B: 0.0 }
	White = Color3{ R: 1.0, G: 1.0, B: 1.0 }
	
	addColor(0.9, 0.0, 0.0)          // red 
	addColor(0.3, 0.3, 0.9)          // blue 
	addColor(0.3, 0.9, 0.3)          // green 
	addColor(0.7, 0.0, 0.8)          // purple  
	addColor(1.0, 0.9, 0.0)          // yellow 
	addColor(1.0, 0.5, 0.0)          // orange 
	addColor(0.0, 1.0, 1.0)          // cyan  
	addColor(Darker, Darker, Darker) // gray 
}


func Ball (ball entities.Ball) {
	var x f32
	var y f32

	gl.Begin(gl.POLYGON)
	gl.Color3f(1.0, 1.0, 1.0)

	var i f32
	for i = 0.0; f32.lt(i, 20.0); i = f32.add(i, 1.0) {
		x = f32.add(ball.x, f32.mul(ball.radius, f32.cos(f32.div(f32.mul(i, geom.TAU), 20.0))))
		y = f32.add(ball.y, f32.mul(ball.radius, f32.sin(f32.div(f32.mul(i, geom.TAU), 20.0))))

		gl.Vertex2f(x, y)
	}

	gl.End()
}


func SetColorByRGB (r f32, g f32, b f32) {
	gl.Color3f(r, g, b)
}


func SetColor3 (color Color3) {
	gl.Color3f(color.R, color.G, color.B)
}


func QuadsBegin () {
	gl.Begin(gl.QUADS)
}
func End () {
	gl.End()
}


// top, right, bottom, left 
func Quad (t f32, r f32, b f32, l f32) {
	gl.Vertex2f(l, t)
	gl.Vertex2f(r, t)
	gl.Vertex2f(r, b)
	gl.Vertex2f(l, b)
}


func SetSequentiallyCycledColor (i i32) {
	var l i32 = len(Colors)
	var c i32 = i % l
	
	gl.Color3f(
		Colors[c].R,
		Colors[c].G,
		Colors[c].B)
}


func addColor (r f32, g f32, b f32) {
	var col Color3
	col = Color3{R: r, G: g, B: b}
	Colors = append(Colors, col)
}
