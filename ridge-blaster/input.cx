package ccInput

import "glfw"

import "app"
import "collision"
import "entity"
import "mesh"



var MousePointer       Vec2
var SnapToGrid         bool
var draggingVertexNode bool
var draggedLayerId     i32
var draggedVertexId    i32

// keys 
var	GLFW_KEY_W      i32 = 87
var	GLFW_KEY_A      i32 = 65
var	GLFW_KEY_S      i32 = 83
var	GLFW_KEY_D      i32 = 68

var	GLFW_KEY_RIGHT  i32 = 262
var	GLFW_KEY_LEFT   i32 = 263
var	GLFW_KEY_DOWN   i32 = 264
var	GLFW_KEY_UP     i32 = 265

var GLFW_KEY_SPACE  i32 = 32
var GLFW_KEY_ESCAPE i32 = 256

//		temp camera control 
var GLFW_KEY_N      i32 = 78
var GLFW_KEY_Y      i32 = 89
var GLFW_KEY_F2     i32 = 291
var GLFW_KEY_F3     i32 = 292



func Setup () () {
	str.print("input/ccInput.Setup()")

	Menus = []Menu{} // prep all menus 
	SetupMenu(EditToolbar, 8) // just 1 

	// callbacks 
	glfw.SetKeyCallback("window", "onKeyEvent")
	glfw.SetMouseButtonCallback("window", "onMouseButtonEvent")
	glfw.SetCursorPosCallback("window", "onCursorPosEvent")
}


func Update () () {
	//str.print("input/ccInput.Update()")

	// motion input status 
	//moveLeft  = keyIsDown(GLFW_KEY_A)
	//moveRight = keyIsDown(GLFW_KEY_D)
	//moveUp    = keyIsDown(GLFW_KEY_W)
	//moveDown  = keyIsDown(GLFW_KEY_S)
	//str.print("call SetPlayerVelocity() causing prob")
}


func GoLeft() (out bool) {
	if i32.eq(glfw.GetKey("window", GLFW_KEY_A), 1) ||
		i32.eq(glfw.GetKey("window", GLFW_KEY_LEFT), 1) {
		out = true
	}
}
func GoRight() (out bool) {
	if i32.eq(glfw.GetKey("window", GLFW_KEY_D), 1) ||
		i32.eq(glfw.GetKey("window", GLFW_KEY_RIGHT), 1) {
		out = true
	}
}
func GoDown() (out bool) {
	if i32.eq(glfw.GetKey("window", GLFW_KEY_S), 1) ||
		i32.eq(glfw.GetKey("window", GLFW_KEY_DOWN), 1) {
		out = true
	}
}
func GoUp() (out bool) {
	if i32.eq(glfw.GetKey("window", GLFW_KEY_W), 1) ||
		i32.eq(glfw.GetKey("window", GLFW_KEY_UP), 1) {
		out = true
	}
}


// mouse buttons 
func onMouseButtonEvent (window str, button i32, action i32, mods i32) () {
	if /* release */ action == 0 {
		if button == 0 {
			draggingVertexNode = false
		}
	} else if /* press */ action == 1 {
		//printf("onMouseButtonEvent() - %f, %f \n", MousePointer.X, MousePointer.Y)

		if button == 0 {
			fireOrActivate()
		} else
		if button == 1 && 
			app.Mode == app.APPMODE_EDITOR &&
			app.EditMode == app.EDITMODE_VERTICES {
			
			str.print("RMB press && EDITMODE_VERTICES")
			var touchedVert bool
			var lId i32 // layer id 
			var i i32
			touchedVert, lId, i = getTouchedVertexNode(MousePointer)
			
			if touchedVert {			
				mesh.RemoveVertexNode(lId, i)
			}
		}
	}
}


// mouse position 
func onCursorPosEvent (window str, x f64, y f64) () {
	var mX f32
	mX = f32.div(f64.f32(x), i32.f32(app.ScreenSizeInPixels.X))
	mX = f32.mul(mX, f64.f32(collision.ScreenEdgeR) * 2.0)
	MousePointer.X = f32.add(f64.f32(collision.ScreenEdgeL), mX)

	var mY f32
	mY = f32.div(f64.f32(y), i32.f32(app.ScreenSizeInPixels.Y))
	mY = f32.mul(mY, 2.0)
	MousePointer.Y = f32.sub(1.0, mY)
	
	if draggingVertexNode {
		mesh.EditLayers[draggedLayerId].Points[draggedVertexId].X = MousePointer.X
		mesh.EditLayers[draggedLayerId].Points[draggedVertexId].Y = MousePointer.Y
	}
	
	//printf("onCursorPosEvent %f, %f \n", MousePointer.X, MousePointer.Y)
}


// keys 
func onKeyEvent (window str, key i32, scancode i32, action i32, mods i32) () {
	// action == 0, release 
	// action == 1, press 
	// action == 2, is an autorepeat event, generated by keys that are held longer than a second

	if action != 2 { // (not a repeat) 
		if action == 1 { // (press) 
			//str.print("-------------------------------------PRESS (not a repeat)")

			//if key == 50 { // GLFW_KEY_2 
			//} else
			//if key == 51 { // GLFW_KEY_3 
			//} else
			if key == GLFW_KEY_ESCAPE {
				app.Mode = app.APPMODE_EDITOR
			} else
			if key == 53 { // GLFW_KEY_5 
				// close 
				str.print("^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^ QUITTING GAME ^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^")
				glfw.SetShouldClose("window", true)
			} else
			if key == GLFW_KEY_SPACE {
				fireOrActivate()
			}

			//maybeToggleMousePointer(key)
		}
	}
}


var wantMousePointer bool
func maybeToggleMousePointer (key i32) () {
	i32.print(key)

	if key == 256 || // GLFW_KEY_ESCAPE 
		key == 257 || // GLFW_KEY_ENTER
		key == 258 || // GLFW_KEY_TAB 
		key == 259 { // GLFW_KEY_BACKSPACE 

		str.print("DEF go to main menu")
		wantMousePointer = !wantMousePointer

		if bool.eq(wantMousePointer, true) {
			str.print("wantMousePointer TRUE")
			glfw.SetInputMode("window", glfw.Cursor, glfw.CursorNormal)
		} else {
			str.print("wantMousePointer FALSE")
			glfw.SetInputMode("window", glfw.Cursor, glfw.CursorDisabled)
		}
	}
}


func fireOrActivate () () {
	//str.print("fireOrActivate ()")
	var mode i32
	var pos  Vec2
	var vel  Vec2
	var ext  Vec2
	
	// handle modes 
	if app.Mode == app.APPMODE_PLAYING { // playing 
		pos = entity.Ents[0].Pos
		pos.X = f32.add(pos.X, entity.Ents[0].Extents.X)
		pos.X = f32.add(pos.X, entity.BulletExtents.X)
		pos.X = f32.add(pos.X, 0.03) // hero to bullet padding 
		vel = Vec2{ X: app.MAX_HERO_SPEED * 2.0, Y: 0.0 }
		mode = entity.ENTMODE_MOVING_STRAIGHT
		ext = entity.BulletExtents
	
		var ent Entity
		ent = Entity{ 
			Type: entity.TYPE_BULLET, 
			Pos: pos, 
			Vel: vel, 
			Mode: mode, 
			Extents: ext }
		
		entity.Add(ent)
	} else if app.Mode == app.APPMODE_EDITOR {
		var touchedMI bool // menu item 
		var i i32
		touchedMI, i = getTouchedMenuItem(MousePointer)
		
		if touchedMI {
			printf("pressed menu item %s \n", Menus[EditToolbar].Items[i].Name)
			
			if i < app.EDITMODE_MAX { // change EditMode
				app.EditMode = i

				if app.EditMode == app.EDITMODE_VERTICES {
					str.print("------> EDITMODE_VERTICES")
				} else
				if app.EditMode == app.EDITMODE_TRIANGLES {
					str.print("------> EDITMODE_TRIANGLES")
				} else
				if app.EditMode == app.EDITMODE_COLORS {
					str.print("------> EDITMODE_COLORS")
				} else
				if app.EditMode == app.EDITMODE_MESH_PICKER {
					str.print("------> EDITMODE_MESH_PICKER")
				}
			} else { // perform one-time actions 
				if i == TOOLBAR_GEN_FUNC {
					mesh.GenerateMeshInitFunc("Char") //"Icon")
				} else
				if i == TOOLBAR_SNAP_TO_GRID {
					SnapToGrid = !SnapToGrid
					
					if SnapToGrid {
						str.print("SNAP_TO_GRID: ON")					
					} else {
						str.print("SNAP_TO_GRID: OFF")
					}
				} else
				if i == TOOLBAR_WIPE_LAYER {
					str.print("-> WIPE_LAYER")
					
					var m Mesh2D 
					m = Mesh2D{}
					mesh.EditLayers[mesh.CurrLayer] = m
				} else
				if i == TOOLBAR_MAINMENU {
					str.print("-> MAINMENU ---------(or WILL take us to main menu when it's implemented)")
					entity.SetupHero()
					app.Mode = app.APPMODE_PLAYING // FIXME: LATER want app.APPMODE_MENU
				}
			}
			
			/////
			/////
			return ///// don't want to use any vertex nodes 
			/////
			/////
		}
		
		var touchedVert bool
		var layerId i32
		touchedVert, layerId, i = getTouchedVertexNode(MousePointer)

		if app.EditMode == app.EDITMODE_TRIANGLES {
			// TODO enhancement: allow player to slide across 3 verts while holding LMB 
			
			if touchedVert {			
				printf("EditLayers[%d].Points[%d] \n", layerId, i)

				if i32.gteq(mesh.PickedTrianglesNextId, 3) {
					mesh.PickedTrianglesNextId = 0
				}

				mesh.PickedTriangle[mesh.PickedTrianglesNextId] = i
				mesh.PickedTrianglesNextId = 
				i32.add(
				mesh.PickedTrianglesNextId, 1)

				if i32.gteq(mesh.PickedTrianglesNextId, 3) {
					mesh.MakeNewEditorTriangle()
				}
			}
		} else if app.EditMode == app.EDITMODE_VERTICES {
			if touchedVert { // drag existing node 
				draggingVertexNode = true
				draggedLayerId  = layerId
				draggedVertexId = i
			} else { // create new node... 
				if MousePointer.X >= -1.0 && // ...when within grid 
					MousePointer.X <= 1.0 &&
					MousePointer.Y >= -1.0 &&
					MousePointer.Y <= 1.0 {
					
					var c Color3
					c = Color3{ 
						R: 0.5, 
						G: 0.5, 
						B: 0.5 }
						
					var m Mesh2D
					m = mesh.EditLayers[mesh.CurrLayer]

					printf("+ vertex: %d - in layer: %d \n", len(m.Points), mesh.CurrLayer)

					m.Colors = append(
					m.Colors, c)
					m.Points = append(
					m.Points, MousePointer)

					mesh.EditLayers[mesh.CurrLayer] = m
				}
			}
		}
	}
}


func getTouchedMenuItem (p Vec2) (touched bool, id i32) {
	touched = false
	
	for i := 0; i32.lt(i, len(Menus[EditToolbar].Items)); i++ {
		var v Vec2
		var e Vec2
		v = Menus[EditToolbar].Items[i].Pos
		e = Menus[EditToolbar].Items[i].Extents
		
		if collision.PointInBox(p, v, e) {			
			touched = true
			id = i			
			break
		}
	}
}


func getTouchedVertexNode (p Vec2) (touched bool, layId i32, vertId i32) {
	touched = false
	
	for li := 0; i32.lt(li, len(mesh.EditLayers)); li++ {
		for i := 0; i32.lt(i, len(mesh.EditLayers[li].Points)); i++ {
			var v Vec2
			v = mesh.EditLayers[li].Points[i]
			
			if collision.PointInBox(p, v, entity.NodeExtents) {			
				touched = true
				layId = li			
				vertId = i			
				break
			}
		}
	}
}
