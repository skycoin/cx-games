package menu

import "app"
import "gameTime"



type MenuItem struct {
	Text  str
	Font  str
	Size  f32  // used for gltext init of the font 
	Left  f32  // in pixels 
	Wid   f32  // w/h caches the resulting string measure queries, 
	Hei   f32  // 		to get the pixel bounds of the text. 
	Hover bool // pointer is touching/hovering-over measured rect 
	Value i32  // only for sliders 
	Max   i32  // (exclusionary) max value for sliders (0 == NOT a slider) 
}
var Items [][]MenuItem // 1st is menu id, 2nd is item id 

// main  
var MainSize            f32  = 150.0
var Play                str  = "Play"
var Options             str  = "Options"
var Controls            str  = "Controls"
var Quit                str  = "Quit"

// options 
var OptionsSize         f32  = 58.0
var ControlsSize        f32  = 58.0
var ShowNextShape       bool = true
var ShowNextShapeText   str  = "Show next shape: "
var SpeedText           str  = "Speed"
var DebrisHeightText    str  = "Debris height"
var DebrisHeight        i32  = 9



func Init () {
 	printf("menu.Init () \n")
 	
 	// app.MODE_MENU_MAIN
 	makeMenuSystemItem(0, MainSize, Play)
 	makeMenuSystemItem(0, MainSize, Options)
 	makeMenuSystemItem(0, MainSize, Controls)
 	makeMenuSystemItem(0, MainSize, Quit)
	// app.MODE_MENU_OPTIONS
 	makeMenuSystemItem(1, OptionsSize, ShowNextShapeText + "YES")
 	makeMenuSystemItem(1, OptionsSize, SpeedText)
 	makeMenuSystemItem(1, OptionsSize, DebrisHeightText)
 	makeMenuSystemItem(1, OptionsSize, Play)
	// app.MODE_MENU_CONTROLS
 	makeMenuSystemItem(2, ControlsSize, Play)

	// sliders (the only items that use these fields) 
	Items[1][1].Max   = gameTime.DROP_SPEED_MAX
	Items[1][1].Value = gameTime.DropSpeed
	Items[1][2].Max   = 11 // for debris height 
	Items[1][2].Value = DebrisHeight
}


func makeMenuSystemItem(menuId i32, size f32, text str) {
	// while menuId is invalid (menu doesn't exist yet) 
	for len(Items) <= menuId {
		printf("making new menu (%d) \n", menuId)

	 	var nm []MenuItem
	 	nm = []MenuItem{}
	 	Items = append(
		Items, nm)
	}
	
 	var mIt MenuItem 
 	mIt = MenuItem{ Size: size, Text: text }
 	Items[menuId] = append(
	Items[menuId], mIt)
}


func GetHoveredItem (menuId i32) (text str, id i32) {
	for i := 0; i < len(Items[menuId]); i++ {			
		if         Items[menuId][i].Hover {
			text = Items[menuId][i].Text
			id   = i
		}
	}
}


func SetSliderValue (i i32, mouseX f32) (out i32) {
	var span f32 = 
		        Items[app.Mode][i].Wid / 
		i32.f32(Items[app.Mode][i].Max)
		
	mouseX = mouseX - Items[app.Mode][i].Left
	Items[app.Mode][i].Value = f32.i32(mouseX / span)
	
	if Items[app.Mode][i].Value < 0 {
		Items[app.Mode][i].Value = 0
	}
	if Items[app.Mode][i].Value >= Items[app.Mode][i].Max {
		Items[app.Mode][i].Value = Items[app.Mode][i].Max - 1
	}
	
	out = Items[app.Mode][i].Value
	//printf("set slider value to: %d \n", out)
}
